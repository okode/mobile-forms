buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:2.1.2'
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.6'
        classpath 'com.github.dcendents:android-maven-gradle-plugin:1.3'
    }
}

repositories {
    jcenter()
}

group = 'com.okode.mobileforms'
version = '1.0.2'

apply plugin: 'com.android.library'
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'

android {
    compileSdkVersion 23
    buildToolsVersion '23.0.3'

    defaultConfig {
        minSdkVersion 23
        targetSdkVersion 23
        versionCode 1
        versionName version
    }

    sourceSets {
        main {
            assets.srcDirs = [ 'src/main/assets', '../../assets' ]
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

def siteUrl = 'https://github.com/okode/mobileforms'
def gitUrl = 'https://github.com/okode/mobileforms.git'
group = 'com.okode.mobileforms'

dependencies {
    compile 'com.android.support:appcompat-v7:23.4.0'
    testCompile 'junit:junit:4.12'
}

install {
    repositories.mavenInstaller {
        pom {
            project {
                packaging 'aar'
                name 'A multiplatform dynamic Web forms generator for mobile apps'
                url siteUrl
                
                // Set your license
                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id 'pjorquera'
                        name 'Pedro Jorquera'
                        email 'pjorquera@okode.com'
                    }
                }
                scm {
                    connection gitUrl
                    developerConnection gitUrl
                    url siteUrl
                }
            }
        }
    }
}

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}
artifacts {
    archives javadocJar
    archives sourcesJar
}

Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())

bintray {
    user = properties.getProperty('bintray.user')
    key = properties.getProperty('bintray.apikey')

    configurations = ['archives']
    publish = true
    pkg {
        repo = 'maven'
        name = 'mobileforms'
        userOrg = 'okode'
        desc = 'A multiplatform dynamic Web forms generator for mobile apps'
        websiteUrl = siteUrl
        vcsUrl = gitUrl
        licenses = ['Apache-2.0']
        labels = ['android', 'java', 'forms']
        publicDownloadNumbers = true
        githubRepo = 'okode/mobileforms'
        githubReleaseNotesFile = 'README.md'
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.13'
}

